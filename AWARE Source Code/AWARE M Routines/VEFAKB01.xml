<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux 5 for x86-64) 2011.1.2 (Build 701)" ts="2014-04-25 16:38:14">
<Routine name="VEFAKB01" type="INT" languagemode="0" timestamp="63287,47897.503181"><![CDATA[
VEFAKB01 ;WOIFO/BT - VEFA AWARE KB EDITOR API CALLED BY CSP PAGE; 01/22/2014
 ;;1.0;VEFA ALRE;**1**;MAY 1, 2011;Build 17
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;;
 ; External Reference DBIA#
 ; ------------------------
 ; #2056   - GET1^DIQ (Supported)
 ; #2056   - GETS^DIQ (Supported)
 ; #2051   - LIST^DIC (Supported)
 ; #10104  - UP^XLFSTR (Supported)
 ;
GETALCAT(VEFARSLT) ; RETURN all ALert Categories
 ; INPUT : NONE
 ; OUTPUT
 ;   VEFARSLT : Reference Array containing all Alert Categories
 ;
 K VEFARSLT
 N FILE S FILE="19008"
 N IENS S IENS=""
 N FIELDS S FIELDS=".01;1;2;3;4"
 N FLAGS S FLAGS=""
 N NUMBER S NUMBER=""
 N FROM,PART
 N INDEX S INDEX="#"
 N SCREEN
 N IDENT S IDENT=""
 N TARGET,ERR
 ;
 D LIST^DIC(FILE,IENS,FIELDS,FLAGS,NUMBER,.FROM,.PART,INDEX,.SCREEN,IDENT,"TARGET","ERR")
 N LAST S LAST=$O(TARGET("DILIST","ID"," "),-1)
 N ID S ID=""
 ;
 F  S ID=$O(TARGET("DILIST","ID",ID)) Q:ID=""  D
 . N IEN S IEN=TARGET("DILIST",2,ID)
 . N NAME S NAME=TARGET("DILIST","ID",ID,.01)
 . N DELALERT S DELALERT=TARGET("DILIST","ID",ID,1)
 . N ACTION S ACTION=TARGET("DILIST","ID",ID,2)
 . S ACTION=$S($$UP^XLFSTR(ACTION)="YES":"true",1:"false")
 . N DESCR S DESCR=TARGET("DILIST","ID",ID,3)
 . N NOTFTYPE S NOTFTYPE=TARGET("DILIST","ID",ID,4)
 . N NOTFDESC S NOTFDESC=NOTFTYPE
 . S:NOTFTYPE'="" NOTFDESC=$P($G(^ORD(100.9,NOTFTYPE,0)),U)
 . N LINE S LINE="  {"
 . S LINE=LINE_"ien:'"_IEN_"',"
 . S LINE=LINE_"name:'"_$$QUOTE^VEFACSP1(NAME)_"',"
 . S LINE=LINE_"descr:'"_$$QUOTE^VEFACSP1(DESCR)_"',"
 . S LINE=LINE_"delAlert:'"_$$QUOTE^VEFACSP1(DELALERT)_"',"
 . S LINE=LINE_"action:'"_ACTION_"',"
 . S LINE=LINE_"notfType:'"_NOTFTYPE_"',"
 . S LINE=LINE_"notfTypeDescr:'"_$$QUOTE^VEFACSP1(NOTFDESC)_"'}"
 . S:ID<LAST LINE=LINE_","
 . S VEFARSLT(ID)=LINE
 QUIT
 ;
GETALTYP(VEFARSLT) ; Return all Alert Types
 K VEFARSLT
 N FILE S FILE="19007"
 N IENS S IENS=""
 N FIELDS S FIELDS=".01;3;4;5;6;7"
 N FLAGS S FLAGS=""
 N NUMBER S NUMBER=""
 N FROM,PART
 N INDEX S INDEX="#"
 N SCREEN
 N IDENT S IDENT=""
 N TARGET,ERR
 ;
 D LIST^DIC(FILE,IENS,FIELDS,FLAGS,NUMBER,.FROM,.PART,INDEX,.SCREEN,IDENT,"TARGET","ERR")
 N LAST S LAST=$O(TARGET("DILIST","ID"," "),-1)
 N ID S ID=""
 ;
 F  S ID=$O(TARGET("DILIST","ID",ID)) Q:ID=""  D
 . N IEN S IEN=TARGET("DILIST",2,ID)
 . N NAME S NAME=TARGET("DILIST","ID",ID,.01)
 . N ACTIVE S ACTIVE=TARGET("DILIST","ID",ID,7)
 . S ACTIVE=$S($$UP^XLFSTR(ACTIVE)="YES":"true",1:"false")
 . N REMDLG S REMDLG=TARGET("DILIST","ID",ID,3)
 . N TIU S TIU=TARGET("DILIST","ID",ID,4)
 . N CAT S CAT=TARGET("DILIST","ID",ID,5)
 . N INITDT S INITDT=TARGET("DILIST","ID",ID,6)
 . N LINE S LINE="  {"
 . S LINE=LINE_"ien:'"_IEN_"',"
 . S LINE=LINE_"name:'"_$$QUOTE^VEFACSP1(NAME)_"',"
 . S LINE=LINE_"active:'"_ACTIVE_"',"
 . S LINE=LINE_"remDlg:'"_$$QUOTE^VEFACSP1(REMDLG)_"',"
 . S LINE=LINE_"tiu:'"_$$QUOTE^VEFACSP1(TIU)_"',"
 . S LINE=LINE_"alertCat:'"_$$QUOTE^VEFACSP1(CAT)_"',"
 . S LINE=LINE_"initDate:'"_INITDT_"'}"
 . S:ID<LAST LINE=LINE_","
 . S VEFARSLT(ID)=LINE
 QUIT
 ;
GETREC(VEFARSLT,FILE,IENS,PRGNOTES) ;get Order Dialog Text for FILE,IENS and stored in VEFARSLT
 QUIT:$G(FILE)=""!($G(IENS)="")
 K VEFARSLT
 S PRGNOTES=""
 N FLDS S FLDS=".01;1;.5;2;1.5;4;3.5;6;.3;.7;5"
 S:FILE'=19007.03 FLDS=FLDS_";7"
 N FLAGS S FLAGS=""
 N TARGET,ERR
 ;
 D GETS^DIQ(FILE,IENS,FLDS,FLAGS,"TARGET","ERR")
 QUIT:'$D(TARGET)
 ;
 N FLDIDX F FLDIDX=.01,1,.5,2,1.5,4,3.5,6,.3,.7,7 S VEFARSLT(FLDIDX)=$$QUOTE^VEFACSP1($G(TARGET(FILE,IENS,FLDIDX)))
 ;
 N NOTE S NOTE=$G(TARGET(FILE,IENS,5))
 QUIT:NOTE=""
 ;
 N ARRTXT M ARRTXT=@NOTE
 N LST S LST=$o(ARRTXT(""),-1)
 N IDX F IDX=1:1:LST S PRGNOTES=PRGNOTES_$$QUOTE^VEFACSP1(ARRTXT(IDX)) I IDX<LST S PRGNOTES=PRGNOTES_"\n"
 QUIT
 ;
GETNAME(IEN) ;return name of Alert Type
 QUIT:$G(IEN)="" ""
 QUIT $$GET1^DIQ(19007,IEN,.01,"E")
]]></Routine>
</Export>
