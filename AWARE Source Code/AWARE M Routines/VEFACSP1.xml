<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux 5 for x86-64) 2011.1.2 (Build 701)" ts="2014-04-25 16:37:55">
<Routine name="VEFACSP1" type="INT" languagemode="0" timestamp="63288,65588.604471"><![CDATA[
VEFACSP1 ;WOIFO/BT - VEFA AWARE COMMON API CALLED BY CSP PAGE ; 01/22/2014
 ;;1.0;VEFA ALRE;**1**;MAY 1, 2011;Build 17
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;;
 ; External Reference DBIA#
 ; ------------------------
 ; #2240  - $$ENCRYP^XUSRB1 (Supported)
 ; #2343  - NAME^XUSER      (Supported)
 ; #2882  - CHECKAV^XUSRB   (Controlled Subs)
 ; #4054  - VALIDAV^XUSRB   (Controlled Subs)
 ; #4440  - $$PROD^XUPROD   (Supported)
 ; #4762  - ^XTV(8989.3,1,"XUS") (Controlled Subs)
 ; #10097 - GETENV^%ZOSV    (Supported)
 ; #10103 - DT^XLFDT        (Supported)
 ; #10104 - UP^XLFSTR       (Supported)
 QUIT
 ;
GETENV(SERVER,VOLBOX,UCI,PROD) ;get environment vars
 ;INPUT
 ;   NONE
 ;OUTPUT
 ;   SERVER  : Local VistA Server
 ;   VOLBOX  : Volume BOX pair
 ;   UCI     : VistA Instance
 ;   PROD    : Production Account Indicator
 ;
 N U S U="^"
 N Y D GETENV^%ZOSV
 S SERVER=$P(Y,U,3)
 S VOLBOX=$P(Y,U,4)
 S UCI=$P(Y,U,1)
 S PROD=$$PROD^XUPROD(1)
 QUIT
 ;
GETDUZ(ACCESS,VERIFY,MSG) ;Get DUZ based on Access and Verify codes
 ;INPUT
 ;  ACCESS  : User Access Code
 ;  VERIFY  : User Verify Code
 ;OUTPUT
 ;  MSG     : Error Message
 ;RETURN
 ;  DUZ  (User ID)
 ;
 S U="^"
 N Y D GETENV^%ZOSV
 N XUENV S XUENV=Y
 N XQVOL,XUVOL S (XQVOL,XUVOL)=$P(Y,U,2)
 N XUCI S XUCI=$P(Y,U,1)
 N AVENC S AVENC=$$ENCRYP^XUSRB1(ACCESS_";"_VERIFY)
 N XWBSTATE S XWBSTATE("XUS XOPT")=$G(^XTV(8989.3,1,"XUS"))
 N RET D VALIDAV^XUSRB(.RET,AVENC)
 S MSG=$G(RET(3))
 QUIT +$G(RET(0))
 ;
GETSITE() ; Get Local VistA site
 QUIT $P(^XTV(8989.3,1,"XUS"),"^",17)
 ;
GETNAMES(VEFARSLT,FILE,SEARCH) ;Get all names for FILE
 ;INPUT
 ;  FILE   : File Number
 ;  SEARCH : Start With value
 ;OUTPUT
 ;  VEFARSLT : Reference Array containing the filtered names for the FILE
 ;
 QUIT:'$D(FILE)
 N IENS S IENS=""
 N FIELDS S FIELDS=".01"
 N FLAGS S FLAGS=""
 N NUMBER S NUMBER=""
 N FROM,PART
 I $D(SEARCH) D
 . S:$L(SEARCH)>1 FROM(1)=$E(SEARCH,1,$L(SEARCH)-1)
 . S PART(1)=SEARCH
 N INDEX S INDEX="B"
 N IDENT S IDENT=""
 N TARGET,ERR
 ;
 D LIST^DIC(FILE,IENS,FIELDS,FLAGS,NUMBER,.FROM,.PART,INDEX,.SCREEN,IDENT,"TARGET","ERR")
 ;
 N ID S ID=""
 ;
 F  S ID=$O(TARGET("DILIST","ID",ID)) Q:ID=""  D
 . N NAME S NAME=$$QUOTE(TARGET("DILIST","ID",ID,.01))
 . S:NAME'="" VEFARSLT(NAME)=""
 QUIT
 ;
QUOTE(STR) ;Replace quotes and line feed characters in STR with HTML chars 
 ;INPUT
 ;  STR : String wirth potential quotes and linefeed
 ;RETURN
 ;  The transformed String with html chars
 ;
 N RESULT S RESULT=""
 N NEWINDEX S NEWINDEX=0
 N INDEX
 ;
 F INDEX=1:1:$L(STR) D
 . I $E(STR,INDEX)=$C(34)!($E(STR,INDEX)="'") D  QUIT
 . . S NEWINDEX=NEWINDEX+1
 . . S $E(RESULT,NEWINDEX)="\"
 . . S NEWINDEX=NEWINDEX+1
 . . S $E(RESULT,NEWINDEX)=$E(STR,INDEX)
 . I $E(STR,INDEX)=$C(10) D  QUIT
 . . S NEWINDEX=NEWINDEX+1
 . . S $E(RESULT,NEWINDEX)="\n"
 . . S NEWINDEX=NEWINDEX+1
 . S NEWINDEX=NEWINDEX+1
 . S $E(RESULT,NEWINDEX)=$E(STR,INDEX)
 ;
 QUIT RESULT
 ;
GETSRCH(VEFARSLT,FILE,SEARCH,FILTER) ;Get all names for FILE start with SEARCH and filter the records with FILEMAN's screen value FILTER
 ;INPUT
 ;  FILE : File Number
 ;  SEARCH : Start with value
 ;  FILTER ; FILEMAN screen value
 ;OUTPUT
 ;  VEFARSLT : Reference Array containing the "filtered" name values
 ;
 QUIT:'$D(FILE)
 QUIT:'$D(SEARCH)
 N IENS S IENS=""
 N FIELDS S FIELDS=".01"
 N FLAGS S FLAGS=""
 N NUMBER S NUMBER=""
 N FROM S:$L(SEARCH)>1 FROM(1)=$E(SEARCH,1,$L(SEARCH)-1)
 N PART S PART(1)=SEARCH
 N INDEX S INDEX="B"
 N IDENT S IDENT=""
 N TARGET,ERR
 N SCREEN 
 S:$G(FILTER)'="" SCREEN=FILTER
 ;
 D LIST^DIC(FILE,IENS,FIELDS,FLAGS,NUMBER,.FROM,.PART,INDEX,.SCREEN,IDENT,"TARGET","ERR")
 ;
 N LAST S LAST=$O(TARGET("DILIST","ID"," "),-1)
 N ID S ID=""
 ;
 F  S ID=$O(TARGET("DILIST","ID",ID)) Q:ID=""  D
 . N IEN S IEN=TARGET("DILIST",2,ID)
 . N NAME S NAME=$$QUOTE(TARGET("DILIST","ID",ID,.01))
 . N LINE S LINE="  {"_"ien:'"_IEN_"',"_"name:'"_NAME_"'}"
 . I ID<LAST S LINE=LINE_","
 . S VEFARSLT(ID)=LINE
 QUIT
 ;
GETNAME(DUZ) ;Return User Name
 QUIT $$UP^XLFSTR($$NAME^XUSER(DUZ,"F"))
 ;
GTDLGIEN(TYPEIEN,FILEIDX,NAME) ; Return Alert Type's Dialog Text IEN
 QUIT $O(^DIZ(19007,TYPEIEN,FILEIDX,"B",NAME,0))
 ;
JSBOOL(BOOLSTR) ;Return javascript boolean
 QUIT $S($$UP^XLFSTR(BOOLSTR)="YES":"true",1:"false")
]]></Routine>
</Export>
