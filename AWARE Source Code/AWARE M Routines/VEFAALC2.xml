<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux 5 for x86-64) 2011.1.2 (Build 701)" ts="2014-04-25 16:35:17">
<Routine name="VEFAALC2" type="INT" languagemode="0" timestamp="63288,50849.632134"><![CDATA[
VEFAALC2 ;WOIFO/BT - VEFA AWARE ALERT CATEGORY API CALLED BY CSP PAGE ; 01/22/2014
 ;;1.0;VEFA ALRE;**1**;MAY 1, 2011;Build 17
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;;
 ; External Reference DBIA#
 ; ------------------------
 ; #2053   - FILE^DIE (Supported)
 ; #2053   - UPDATE^DIE (Supported)
 ;
SAVE(TODO,IEN,RECORD,ERR) ;save Alert Category Record, return 1 (success), 0 (fail)
 ; INPUT
 ;   TODO   : add or edit
 ;   IEN    : ID of Alert Category Record to save
 ;   RECORD : Alert Category Record to save
 ; OUTPUT
 ;   ERR    : Error Message
 ; RETURN
 ;   1 - success
 ;   0 - failed
 ;
 S ERR=""
 N NAME S NAME=$P(RECORD,"^")
 N DESCR S DESCR=$P(RECORD,"^",2)
 N DELALERT S DELALERT=$P(RECORD,"^",3)
 N NOTFTYPE S NOTFTYPE=$P(RECORD,"^",4)
 N ACTION S ACTION=$P(RECORD,"^",5)
 N FDA,MSG
 ;
 I TODO="edit" D
 . I IEN="" S ERR="Invalid IEN" QUIT
 . I '$D(^DIZ(19008,IEN)) S ERR="Record doesn't exist" QUIT
 . S FDA(19008,IEN_",",1)=DELALERT
 . S FDA(19008,IEN_",",2)=ACTION
 . S FDA(19008,IEN_",",3)=DESCR
 . S FDA(19008,IEN_",",4)=NOTFTYPE
 . D FILE^DIE("E","FDA","MSG")
 ;
 I TODO="add" D
 . I NAME="" S ERR="Invalid Name" QUIT
 . I $D(^DIZ(19008,"B",NAME)) S ERR="<"_NAME_"> exists!" QUIT
 . S FDA(19008,"+1,",.01)=NAME
 . S FDA(19008,"+1,",1)=DELALERT
 . S FDA(19008,"+1,",2)=ACTION
 . S FDA(19008,"+1,",3)=DESCR
 . S FDA(19008,"+1,",4)=NOTFTYPE
 . D UPDATE^DIE("E","FDA","","MSG")
 ;
 QUIT:(ERR'="") 0
 ;
 I $D(MSG) D
 . N ERNO S ERNO=$O(MSG("DIERR",0))
 . S ERR=MSG("DIERR",ERNO,"TEXT",1)
 ;
 I '$D(MSG),TODO="add",NAME'="" S IEN=$O(^DIZ(19008,"B",NAME,0))
 ;
 Q (ERR="")
 ;
GETDELOP(DELOPTS) ;Get Alert Category Delete Options
 ; INPUT
 ;   NONE
 ; OUTPUT
 ;   DELOPTS : Reference arrays containing Alert Delete options
 ;
 S U = "^"
 N OPTIONS S OPTIONS=$P(^DD(19008,1,0),U,3)
 N IOPT
 F IOPT=1:1:$L(OPTIONS,";") D
 . N OPT S OPT=$P(OPTIONS,";",IOPT)
 . I OPT'="" D
 . . N DESCR S DESCR=$P(OPT,":",2)
 . . S DELOPTS(IOPT)=DESCR ;"<option value='"_DESCR_"'>"_DESCR_"</option>"
 Q
]]></Routine>
</Export>
