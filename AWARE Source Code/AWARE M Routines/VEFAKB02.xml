<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux 5 for x86-64) 2011.1.2 (Build 701)" ts="2014-04-25 16:38:28">
<Routine name="VEFAKB02" type="INT" languagemode="0" timestamp="63288,57007.987179"><![CDATA[
VEFAKB02 ;WOIFO/BT - VEFA AWARE KB EDITOR ALERT TYPE API CALLED BY CSP PAGE; 01/22/2014
 ;;1.0;VEFA ALRE;**1**;MAY 1, 2011;Build 17
 ;; Per VHA Directive 2004-038, this routine should not be modified.
 ;;
 ; External Reference DBIA#
 ; ------------------------
 ; #2051   - LIST^DIC (Supported)
 ; #2053   - FILE^DIE (Supported)
 ; #2053   - UPDATE^DIE (Supported)
 ; #2056   - GETS^DIQ (Supported)
 ; #10103  - FMTE^XLFDT (Supported)
 ; #10103  - DT^XLFDT (Supported)
 ; #10104  - UP^XLFSTR (Supported)
 ;
GETATFRM(VEFARSLT,IENS,DTINIT,ACTIVE,SITE) ;get Alert Type info and stored in VEFARSLT
 ;INPUT
 ;  IENS : Record ID of the Alert Type
 ;OUTPUT
 ;  VEFARSLT : Reference Array by field contaiing the field value
 ;  DTINIT  : Initiation Date
 ;  ACTIVE  : Alert Type active (true/false) indicator
 ;  SITE   : Site Number where the Alert Type is created
 ;
 K VEFARSLT
 QUIT:$G(IENS)=""
 N FILE S FILE=19007
 S PRGNOTES=""
 N FLDS S FLDS=".01;3;4;5;6;7;8;9;10"
 N FLAGS S FLAGS="EI"
 N TARGET,ERR
 ;
 D GETS^DIQ(FILE,IENS,FLDS,FLAGS,"TARGET","ERR")
 QUIT:'$D(TARGET)
 ;
 N FLDIDX F FLDIDX=.01,3:1:5,8,9,10 S VEFARSLT(FLDIDX)=$$QUOTE^VEFACSP1($G(TARGET(FILE,IENS,FLDIDX,"E")))
 S DTINIT=$G(TARGET(FILE,IENS,6,"I"))
 S:DTINIT'="" DTINIT=$$FMTE^XLFDT(DTINIT,5)
 S ACTIVE=$G(TARGET(FILE,IENS,7,"I"))
 S ACTIVE=$S(ACTIVE:"true",1:"false")
 S SITE=$G(TARGET(FILE,IENS,8,"I"))
 ;S SITE=SITE_" - "_$P(^DIC(4,SITE,0),U)
 QUIT
 ;
GETATLST(VEFARSLT,FILE,IENS) ;get all dialogs (oreder/follow-up/comments) for FILE's IENS
 ;INPUT
 ;  FILE : Alert Type Sub file (Order/follow-up/comments)
 ;  IENS : Alert Type Record ID
 ;OUTPUT
 ;  VEFARSLT : Reference Array contaiing all dialog text records
 ;
 K VEFARSLT
 QUIT:$G(FILE)=""
 QUIT:$G(IENS)=""
 N FIELDS S FIELDS=".01;1;.5;2;1.5;4;3.5"
 N FLAGS S FLAGS=""
 N NUMBER S NUMBER=""
 N FROM,PART
 N INDEX S INDEX="#"
 N SCREEN
 N IDENT S IDENT=""
 N TARGET,ERR
 ;
 D LIST^DIC(FILE,IENS,FIELDS,FLAGS,NUMBER,.FROM,.PART,INDEX,.SCREEN,IDENT,"TARGET","ERR")
 N LAST S LAST=$O(TARGET("DILIST","ID"," "),-1)
 N ID S ID=""
 ;
 F  S ID=$O(TARGET("DILIST","ID",ID)) Q:ID=""  D
 . N IEN S IEN=TARGET("DILIST",2,ID)
 . N NAME S NAME=TARGET("DILIST","ID",ID,.01)
 . N DISPGRP S DISPGRP=TARGET("DILIST","ID",ID,1)
 . N ITMNAME S ITMNAME=TARGET("DILIST","ID",ID,.5)
 . N DLGGRP S DLGGRP=TARGET("DILIST","ID",ID,2)
 . N ORDDLG S ORDDLG=TARGET("DILIST","ID",ID,1.5)
 . N ADDFIND S ADDFIND=TARGET("DILIST","ID",ID,4)
 . N PKG S PKG=TARGET("DILIST","ID",ID,3.5)
 . N LINE S LINE="  {"
 . S LINE=LINE_"ien:'"_IEN_"',"
 . S LINE=LINE_"boxText:'"_$$QUOTE^VEFACSP1(NAME)_"',"
 . S LINE=LINE_"ordItemDispGrp:'"_$$QUOTE^VEFACSP1(DISPGRP)_"',"
 . S LINE=LINE_"ordItemName:'"_$$QUOTE^VEFACSP1(ITMNAME)_"',"
 . S LINE=LINE_"ordDlgDispGrp:'"_$$QUOTE^VEFACSP1(DLGGRP)_"',"
 . S LINE=LINE_"ordDlg:'"_$$QUOTE^VEFACSP1(ORDDLG)_"',"
 . S LINE=LINE_"AddFind:'"_$$QUOTE^VEFACSP1(ADDFIND)_"',"
 . S LINE=LINE_"OrdDlgPkg:'"_$$QUOTE^VEFACSP1(PKG)_"'}"
 . S:ID<LAST LINE=LINE_","
 . S VEFARSLT(ID)=LINE
 QUIT
 ;
SAVATFRM(TODO,FILE,IEN,RECORD,MSG) ;save alert type reminder dialog text
 ;INPUT
 ;  TODO   : add or edit
 ;  FILE   : File Number 
 ;  IEN    : ID of Alert Type to save
 ;  RECORD : Alert Type Record to save
 ;OUTPUT
 ;  MSG    : Error Message
 ;RETURN
 ;  1 - success
 ;  0 - failed
 ;
 I $G(FILE)="" S MSG="Invalid FILE" QUIT 0
 I $G(IEN)="" S MSG="Invalid IEN" QUIT 0
 N NAME S NAME=$P(RECORD,"^",1)
 N REMDLG S REMDLG=$P(RECORD,"^",2)
 N TIU S TIU=$P(RECORD,"^",3)
 N CAT S CAT=$P(RECORD,"^",4)
 N INITDT S INITDT=$P(RECORD,"^",5)
 N MNE S MNE=$P(RECORD,"^",6)
 N SITE S SITE=$P(RECORD,"^",7) ;$P(^XTV(8989.3,1,"XUS"),"^",17)
 N ACTIVE S ACTIVE=$p(RECORD,"^",8)
 N DESCR S DESCR=$p(RECORD,"^",9)
 N ERR,FDA
 ;
 S FDA(FILE,IEN_",",.01)=NAME
 S FDA(FILE,IEN_",",3)=REMDLG
 S FDA(FILE,IEN_",",4)=TIU
 S FDA(FILE,IEN_",",5)=CAT
 S FDA(FILE,IEN_",",7)=ACTIVE
 S FDA(FILE,IEN_",",8)=SITE
 S FDA(FILE,IEN_",",9)=MNE
 S FDA(FILE,IEN_",",10)=DESCR
 ;
 I TODO="edit" D FILE^DIE("E","FDA","ERR")
 I TODO="add" D
 . S FDA(FILE,IEN_",",6)=INITDT
 . D UPDATE^DIE("E","FDA","","ERR")
 ;
 I '$D(ERR) D
 . I NAME="@" S MSG="Alert Type is deleted successfully"
 . I NAME'="@" S MSG="Alert Type is "_$S(TODO="add":"added",1:"updated")_" successfully"
 I $D(ERR) D
 . N ERNO S ERNO=$O(ERR("DIERR",0))
 . S MSG=ERR("DIERR",ERNO,"TEXT",1)
 ;
 QUIT '$D(ERR)
 ;
SAVDLG(TODO,FILE,RECORD,TYPEIEN,FILEIDX,IEN,MSG) ; save order/followup/comments dialog
 ;INPUT
 ;  TODO    : add or edit
 ;  FILE    : Sub File Number 
 ;  RECORD  : Alert Type Record to save
 ;  TYPEIEN : Record ID of Alert Type to save
 ;  FILEIDX : Subscript Index representing the Sub File
 ;  IEN     : Record ID of the dialog text
 ;OUTPUT
 ;  MSG     : Error Message
 ;RETURN
 ;  1 - success
 ;  0 - failed
 ;
 I $G(FILE)=""!($G(FILEIDX)="") S MSG="Invalid FILE" QUIT 0
 I $G(TYPEIEN)="" S MSG="Invalid Alert Type IEN" QUIT 0
 S IEN=$G(IEN)
 N NAME S NAME=$P(RECORD,"^",1)
 N DISPGRP S DISPGRP=$P(RECORD,"^",2)
 N ITMNAME S ITMNAME=$P(RECORD,"^",3)
 N DLGGRP S DLGGRP=$P(RECORD,"^",4)
 N ORDDLG S ORDDLG=$P(RECORD,"^",5)
 N ADDFIND S ADDFIND=$P(RECORD,"^",6)
 N PKG S PKG=$P(RECORD,"^",7)
 N CMT S CMT=$P(RECORD,"^",8)
 N DESIRED S DESIRED=$P(RECORD,"^",9)
 N ITEMREQ S ITEMREQ=$P(RECORD,"^",10)
 ;N PRGNOTE S PRGNOTE=$P(RECORD,"^",11)
 ;
 N ERR,FDA,IENS
 I TODO="edit" S IENS=IEN_","_TYPEIEN
 I TODO="add" S IENS="+1,"_TYPEIEN
 S FDA(FILE,IENS_",",.01)=NAME
 S FDA(FILE,IENS_",",1)=DISPGRP
 S FDA(FILE,IENS_",",.5)=ITMNAME
 S FDA(FILE,IENS_",",2)=DLGGRP
 S FDA(FILE,IENS_",",1.5)=ORDDLG
 S FDA(FILE,IENS_",",4)=ADDFIND
 S FDA(FILE,IENS_",",3.5)=PKG
 S FDA(FILE,IENS_",",6)=CMT
 S FDA(FILE,IENS_",",.3)=DESIRED
 S FDA(FILE,IENS_",",.7)=ITEMREQ
 ;
 I TODO="edit" D FILE^DIE("E","FDA","ERR")
 I TODO="add" D UPDATE^DIE("E","FDA","","ERR")
 ;
 I $D(ERR) D
 . N ERNO S ERNO=$O(ERR("DIERR",0))
 . S MSG=ERR("DIERR",ERNO,"TEXT",1)
 QUIT:$D(ERR) 0
 ;
 S:NAME="@" MSG="Dialog Text is deleted successfully"
 QUIT:NAME="@" 1
 ;
 S MSG="Dialog Text is "_$S(TODO="add":"added",1:"updated")_" successfully"
 I TODO="add" S IEN=$O(^DIZ(19007,TYPEIEN,FILEIDX,"B",NAME,0))
 ;
 ;I IEN D
 ;. N WPTXT S WPTXT(1)=PRGNOTE
 ;. S IENS=IEN_ ","_TYPEIEN
 ;. D WP^DIE(FILE,IENS_",",5,"","WPTXT","ERR")
 ;. I $D(ERR) S MSG="Problem saving WP field",IEN=""
 ;
 QUIT IEN
 ;
INACTV(NAME) ;deactivate Alert Type - caller must provide Alert Type NAME value
 ;INPUT
 ;  NAME : Alert Type Name
 ;OUTPUT
 ;  ACTIVE field of Alert Type set to 0 (false)
 ;
 QUIT:$G(NAME)=""
 N IEN S IEN=$O(^DIZ(19007,"B",NAME,0))
 QUIT:'IEN
 N FDA S FDA(19007,IEN_",",7)=0
 N ERR D FILE^DIE("","FDA","ERR")
 QUIT
 ;
UPINITDT(NAME) ;Update Initiation Date
 ;INPUT
 ;  NAME : Alert Type Name
 ;OUTPUT
 ;  Initiation Date field of Alert Type set to the current date
 ;
 QUIT:$G(NAME)=""
 N IEN S IEN=$O(^DIZ(19007,"B",NAME,0))
 QUIT:'IEN
 N U S U="^"
 N DTINIT S DTINIT=$P($G(^DIZ(19007,IEN,3)),U,4)
 QUIT:(DTINIT'="")
 N ACTIVE S ACTIVE=$P($G(^DIZ(19007,IEN,3)),U,5)
 QUIT:'ACTIVE
 N FDA S FDA(19007,IEN_",",6)=$$DT^XLFDT
 N ERR D FILE^DIE("","FDA","ERR")
 QUIT
 ;
DELORDLG(FILE,TYPEIEN,ORDIEN) ;delete order dialog box text
 ;INPUT
 ;  FILE   :  Alert Type Sub File for (Order/Follow-up/Comments) dialog
 ;  TYPIEN : Alert Type Record ID (IEN)
 ;  ORDIEN : Alert Type Dialog Record ID
 ;OUTPUT
 ;  Alert Type Dialog Record with IENS (TYPIEN, ordien) removed from Alert Type file
 ;
 N IENS S IENS=ORDIEN_","_TYPEIEN
 N FDA S FDA(FILE,IENS_",",.01)="@"
 N ERR D FILE^DIE("","FDA","ERR")
 ;
 N MSG S MSG="Dialog Text is deleted successfully"
 I $D(ERR) D
 . N ERNO S ERNO=$O(ERR("DIERR",0))
 . S MSG=ERR("DIERR",ERNO,"TEXT",1)
 ;
 QUIT MSG
 ;
DELALTYP(TYPIEN,MSG) ;delete Alert Type
 ;INPUT
 ;   TYPIEN : Alert Type Record ID (IEN)
 ; OUTPUT
 ;   Alert Type with TYPIEN removed from Alert Type file
 ;
 N FDA S FDA(19007,TYPIEN_",",.01)="@"
 N ERR D FILE^DIE("","FDA","ERR")
 ;
 S MSG="Alert Type is deleted successfully"
 I $D(ERR) D
 . N ERNO S ERNO=$O(ERR("DIERR",0))
 . S MSG=ERR("DIERR",ERNO,"TEXT",1)
 ;
 QUIT '$D(ERR)
 ;
GETATIEN(NAME) ; Given Name, return Alert Type IEN
 QUIT $O(^DIZ(19007,"B",NAME,0))	

]]></Routine>
</Export>
