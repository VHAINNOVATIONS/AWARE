<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux 5 for x86-64) 2011.1.2 (Build 701)" ts="2014-04-25 17:05:13">
<Class name="VEFA.AlertType">
<ProcedureBlock>0</ProcedureBlock>
<Super>%CSP.Page</Super>
<TimeChanged>63288,65601.148314</TimeChanged>
<TimeCreated>63208,82670.891452</TimeCreated>

<Method name="Populate">
<Description>
Populate HTML Reminder Dialog elements with Alert type's Reminder information
Populate HTML Grid element with Order dialog (multiple records)
Populate HTML Grid element with Follow-up dialog (multiple records)
Populate HTML Grid element with Comments dialog (multiple records)</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	Q:##class(VEFA.Login).InvalidPage()
	;
	N TODO S TODO=%request.GetCookie("type.todo")
	;
	W "<script type='text/javascript'>"
	;
    D ##class(VEFA.Login).Selector("19008","#selAlertCat")
	;    
    I TODO="edit" D
	. D ..PopulateAlertTypeForm()
	. D ..FillAlertTypeText("ord", "19007.01", "fillOrdDlg", "OrdDlgData", "#listOrder")
	. D ..FillAlertTypeText("fol", "19007.02", "fillfollDlg", "follDlgData", "#listFol")
	. D ..FillAlertTypeText("cmt", "19007.03", "fillCmtDlg", "CmtDlgData", "#listCmt")
	I TODO="add" D ..PopulateEmptyAlertTypeForm()
    ;
    W "</script>"
]]></Implementation>
</Method>

<Method name="PopulateEmptyAlertTypeForm">
<Description>
Populate HTML Reminder Dialog elements with empty information during adding new record</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	N FIELDS
	S FIELDS(.01)="txtName"
	S FIELDS(3)="txtRemDlg"
	S FIELDS(4)="txtTIU"
	S FIELDS(5)="selAlertCat"
	S FIELDS(9)="txtMnemonic"
	S FIELDS(10)="txtDescr"
	;
	W "function fillForm() {"
	N PAGE S PAGE=%request.GetCookie("fromPage")
	;
	I PAGE="saveAlertType" D
	. N REC S REC=..UnescapeURL(%request.GetCookie("type.record"))
	. W !,"$('#txtName').val('"_$P(REC,"^",1)_"');"
	. W !,"$('#txtRemDlg').val('"_$P(REC,"^",2)_"');"
	. W !,"$('#txtTIU').val('"_$P(REC,"^",3)_"');"
	. W !,"$('#selAlertCat').val('"_$P(REC,"^",4)_"');"
	. W !,"$('#dtInit').val('"_$P(REC,"^",5)_"');"
	. W !,"$('#txtMnemonic').val('"_$P(REC,"^",6)_"');"
	. W !,"$('#txtSite').val('"_$P(REC,"^",7)_"');"
	. N ACTIVE S ACTIVE=$S($P(REC,"^",8):"true",1:"false")
	. W "$('#chkActive').prop('checked',"_ACTIVE_");"
	. W !,"$('#txtDescr').val('"_$P(REC,"^",9)_"');"
	I PAGE'="saveAlertType" D
	. N IDX F IDX=.01,3:1:5,9,10 W !,"$('#",FIELDS(IDX),"').val('');"
	. W "$('#dtInit').val('');"
	. W "$('#chkActive').prop('checked',false);"
	;
	N SITE S SITE=$$GETSITE^VEFACSP1()
	W "$('#txtSite').val('",SITE,"');"
	W "}"
]]></Implementation>
</Method>

<Method name="PopulateAlertTypeForm">
<Description>
Populate HTML Reminder Dialog elements with Alert type's Reminder information</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	N FIELDS
	S FIELDS(.01)="txtName"
	S FIELDS(3)="txtRemDlg"
	S FIELDS(4)="txtTIU"
	S FIELDS(5)="selAlertCat"
	S FIELDS(9)="txtMnemonic"
	S FIELDS(10)="txtDescr"
	N IENS S IENS=%request.GetCookie("type.ien")
	;
	N REC,DTINIT,ACTIVE,SITE
	D GETATFRM^VEFAKB02(.REC,IENS_",",.DTINIT,.ACTIVE,.SITE) ;get Alert Type info and stored in VEFARSLT
	;
	W "function fillForm() {"
	N IDX F IDX=.01,3:1:5,9,10 W !,"$('#",FIELDS(IDX),"').val('",REC(IDX),"');"
	W "$('#dtInit').val('",DTINIT,"');"
	W "$('#dtInit').attr('disabled',true);"
	W "$('#chkActive').prop('checked',",ACTIVE,");"
	W "$('#txtSite').val('",SITE,"');"
	W "}"
]]></Implementation>
</Method>

<Method name="FillAlertTypeText">
<Description>
Populate HTML Grid element with Order, Followup, or comments dialog (multiple records)</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>PREFIX,FILE,FUNC,DATANM,GRIDNM</FormalSpec>
<Implementation><![CDATA[
	N IENS S IENS=","_%request.GetCookie("type.ien")_","
	N DATA D GETATLST^VEFAKB02(.DATA,FILE,IENS)
	;	
	W "function ",FUNC,"() {"
	W "  var ",DATANM," = ["
	N IDX S IDX="" F  S IDX=$O(DATA(IDX)) Q:IDX=""  W DATA(IDX)
	W "	];"
	;
	W "  for(var i=0;i<",DATANM,".length;i++)"
	W "    $('",GRIDNM,"').jqGrid('addRowData',i+1, ",DATANM,"[i]);"
	W "  if (",DATANM,".length>0) {"
	W "     $('",GRIDNM,"').focus();"
	W "     $('",GRIDNM,"').jqGrid('setSelection',1);"
	W "  }"
	W "}"
]]></Implementation>
</Method>

<Method name="Save">
<Description>
Save or Delete Alert Type and return result web page</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	QUIT:##class(VEFA.Login).InvalidPage()
	;
	N TODO S TODO=%request.GetCookie("type.todo")
	I TODO="delete" D
	. D ..DeleteAlertType()
	I TODO'="delete" D
	. N OK S OK=..SaveReminderDlg()
	. I OK D ..UpdateInitDate()
	. I 'OK D ..DeactivateAlertType()
]]></Implementation>
</Method>

<Method name="DeleteAlertType">
<Description>
Delete Alert Type and return result web page</Description>
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	N TYPIEN S TYPIEN=%request.GetCookie("type.ien")
	N OK,MSG S OK=$$DELALTYP^VEFAKB02(TYPIEN,.MSG)
	D ##class(VEFA.Login).DisplayResult(OK, MSG)
]]></Implementation>
</Method>

<Method name="SaveReminderDlg">
<Description>
Save Alert Type's Reminder dialog section
Validate Alert Type  and return result web page
Display result web page</Description>
<ClassMethod>1</ClassMethod>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	N REC S REC=..UnescapeURL(%request.GetCookie("type.record"))
	N NAME S NAME=$P(REC,"^")
	;
	I NAME="" D ##class(VEFA.Login).DisplayResult(0, "Invalid Name!")
	QUIT:NAME="" 0
	;	
	N MSG,IEN S IEN=..FileAlertType(REC,.MSG)
    I IEN="" D ##class(VEFA.Login).DisplayResult(0,MSG)
    QUIT:IEN="" 0
    ;
	I NAME="@" D ##class(VEFA.Login).DisplayResult(1,MSG)
	QUIT:NAME="@" 1
	;
	D ##class(VEFA.Login).SetCookie("type.ien",IEN)
	N ER,VALID S VALID=..ValidateAlertType(NAME,.ER)
	;
	N RSLT S RSLT=$S(VALID:MSG,1:ER)
	D ##class(VEFA.Login).DisplayResult(VALID,RSLT)
	D ##class(VEFA.Login).SetCookie("updateResult",RSLT)
	;
	N TODO S TODO=%request.GetCookie("type.todo")
	I 'VALID,TODO="add" D 
	. D ##class(VEFA.Login).SetCookie("type.todo","edit")
	. D ##class(VEFA.Login).SetCookie("type.id","")
	;
	QUIT VALID
]]></Implementation>
</Method>

<Method name="FileAlertType">
<Description>
Save Alert Type's Reminder dialog section and return error message if failed</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[RECORD:%String,&MSG:%String]]></FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	S MSG=""
	N NAME S NAME=$P(RECORD,"^")
	N TODO S TODO=%request.GetCookie("type.todo")
	N DBIEN S DBIEN=$$GETATIEN^VEFAKB02(NAME)
	N FILE S FILE=19007
	N IEN
	;
	I TODO="edit" D
	. S IEN=%request.GetCookie("type.ien")
	. I DBIEN,IEN'=DBIEN S MSG="<"_NAME_"> exists!"
	I TODO="add" D
	. S IEN="+1"
	. I DBIEN S MSG="<"_NAME_"> exists!"
	;
	QUIT:(MSG'="") ""
	;			
	N OK S OK=$$SAVATFRM^VEFAKB02(TODO,FILE,IEN,RECORD,.MSG)
	I TODO="add" D
	. I OK D
	. . S IEN=$$GETATIEN^VEFAKB02(NAME)
	. . I IEN D ##class(VEFA.Login).SetCookie("type.ien",IEN)
	. I 'OK S IEN=""
	;
	QUIT IEN
]]></Implementation>
</Method>

<Method name="ValidateAlertType">
<Description>
Validate Alert Type and return result</Description>
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[NAME:%String,&ER:%String]]></FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	D BASICCHK^VEFAALR4(.ER,NAME)
	QUIT (ER = "OK ALL VALIDITY CHECKS PASSED")
]]></Implementation>
</Method>

<Method name="SaveDlgForm">
<ClassMethod>1</ClassMethod>
<FormalSpec>FROMPAGE,PREFIX,FILE,FILEIDX</FormalSpec>
<Implementation><![CDATA[
	N OK S OK=..SaveOrderDlg(FROMPAGE, PREFIX, FILE, FILEIDX)
	D:'OK ..DeactivateAlertType()
]]></Implementation>
</Method>

<Method name="DeleteDlgForm">
<ClassMethod>1</ClassMethod>
<FormalSpec>FROMPAGE,PREFIX,FILE,FILEIDX</FormalSpec>
<Implementation><![CDATA[
	N OK S OK=..DeleteOrderDlg(PREFIX, FILE)
	D:'OK ..DeactivateAlertType()
]]></Implementation>
</Method>

<Method name="DeactivateAlertType">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	N REC S REC=..UnescapeURL(%request.GetCookie("type.record"))
	N NAME S NAME=$P(REC,"^")
	D INACTV^VEFAKB02(NAME)
]]></Implementation>
</Method>

<Method name="UpdateInitDate">
<ClassMethod>1</ClassMethod>
<Implementation><![CDATA[
	N REC S REC=..UnescapeURL(%request.GetCookie("type.record"))
	N NAME S NAME=$P(REC,"^")
	D UPINITDT^VEFAKB02(NAME)
]]></Implementation>
</Method>

<Method name="SaveOrderDlg">
<ClassMethod>1</ClassMethod>
<FormalSpec>FROMPAGE,PREFIX,FILE,FILEIDX</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	N TYPEIEN S TYPEIEN=%request.GetCookie("type.ien")
	N RECORD S RECORD=..UnescapeURL(%request.GetCookie("type.record"))
	N NAME S NAME=$P(RECORD,"^",1)
	;
	N MSG,IEN S IEN=..FileAlertType(RECORD,.MSG)
	I IEN="" D ##class(VEFA.Login).DisplayResult(0,MSG)
	QUIT:IEN="" 0
	;
	D ##class(VEFA.Login).SetCookie("type.ien",IEN)
	S IEN=..SaveDialogText(FROMPAGE,TYPEIEN,PREFIX,FILE,FILEIDX,.MSG)
	I 'IEN D ##class(VEFA.Login).DisplayResult(0,MSG)
	QUIT:'IEN 0
	;
	N ER,VALID S VALID=..ValidateAlertType(NAME,.ER)
	S RSLT=$S(VALID:MSG,1:ER)
	;
	N REQ S REQ=%request.GetCookie(PREFIX_".form")
	D ##class(VEFA.Login).DisplayResult(VALID,RSLT)
	D ##class(VEFA.Login).SetCookie("updateResult",RSLT)
	;
	QUIT VALID
]]></Implementation>
</Method>

<Method name="DeleteOrderDlg">
<ClassMethod>1</ClassMethod>
<FormalSpec>PREFIX,FILE</FormalSpec>
<ReturnType>%Boolean</ReturnType>
<Implementation><![CDATA[
	N TYPEIEN S TYPEIEN=%request.GetCookie("type.ien")
	N ORDIEN S ORDIEN=%request.GetCookie(PREFIX_".ien")
	N MSG S MSG=$$DELORDLG^VEFAKB02(FILE,TYPEIEN,ORDIEN)
	D ##class(VEFA.Login).DisplayResult(0,MSG)
	QUIT 0
]]></Implementation>
</Method>

<Method name="SaveDialogText">
<ClassMethod>1</ClassMethod>
<FormalSpec><![CDATA[FROMPAGE,TYPEIEN,PREFIX,FILE,FILEIDX,&MSG]]></FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	S MSG=""
	N RECORD S RECORD=..UnescapeURL(%request.GetCookie("dlg.record"))
	N NAME S NAME=$P(RECORD,"^",1)
	;
	I NAME="" S MSG="Invalid Name!"
	QUIT:NAME="" ""
	;
	N DBIEN S DBIEN=$$GTDLGIEN^VEFACSP1(TYPEIEN,FILEIDX,NAME)
	N TODO S TODO=%request.GetCookie(PREFIX_".todo")
	N IEN S IEN=""
	;
	I TODO="edit" D
	. S IEN=%request.GetCookie(PREFIX_".ien")
	. I DBIEN,IEN'=DBIEN S MSG="<"_NAME_"> exists!",IEN=""
	I TODO="add",DBIEN S MSG="<"_NAME_"> exists!"
	QUIT:(MSG'="") ""
	;
	N OK S OK=$$SAVDLG^VEFAKB02(TODO,FILE,RECORD,TYPEIEN,FILEIDX,.IEN,.MSG)
	I OK,TODO="add" D ##class(VEFA.Login).SetCookie(PREFIX_".ien",IEN)
	;	
	QUIT IEN
]]></Implementation>
</Method>
</Class>
</Export>
